/* Estacionamento_r2
 * Author: Saul
 * Creation date: 25/10/2016
 */

REFINEMENT Estacionamento_r2
REFINES Estacionamento_r1

SEES TiposComuns


ABSTRACT_VARIABLES
    cv , // Contador de Vagas
    tipo ,
    status ,
    ativo

INVARIANT
    ativo : VAGAS --> BOOL &

    card ( ativo |> { TRUE } ) = cv &
    dom ( ativo |> { TRUE } ) = dom ( tipo ) &
    dom ( ativo |> { TRUE } ) = dom ( status )

INITIALISATION
    cv     := 0 ;
    tipo   := {} ;
    status := {} ;
    ativo  := VAGAS * { FALSE }

OPERATIONS
    // Controle de VAGAS
    criar ( tt ) =
        PRE cv < MAX_INT THEN
            cv := cv + 1 ;
            ANY vv WHERE
                vv : VAGAS        &
                vv /: dom ( tipo ) &
                vv /: dom ( status ) &
                ativo ( vv ) = FALSE
            THEN
                ativo ( vv ) := TRUE ;
                tipo ( vv ) := tt ;
                status ( vv ) := livre
            END
        END ;

    excluir ( vv ) =
        PRE cv > 0 &
            ativo ( vv ) = TRUE
        THEN
            cv := cv - 1 ;
            ativo ( vv ) := FALSE ;
            tipo   := { vv } <<| tipo ;
            status := { vv } <<| status
        END ;

    // Ocupação e liberação de VAGAS
    ocupar ( vv ) = status ( vv ) := ocupada ;

    liberar ( vv ) = status ( vv ) := livre ;

    // Consultas
    cc <-- get_cor_lampada ( vv ) =
        BEGIN
            IF status ( vv ) = livre THEN
                CASE tipo ( vv ) OF EITHER idoso THEN
                    cc := azul
                OR deficiente THEN
                    cc := amarela
                ELSE // comum
                    cc := verde
                END
                END
            ELSE // status(vv) = ocupada
                cc := vermelha
            END
        END ;

    tt <-- get_tipo_vaga ( vv ) = tt := tipo ( vv ) ;

    /* qc: Quantidade de VAGAS Comuns
       oc: quantidade de VAGAS Ocupadas Comuns
       as demais são análogas para deficientes e idosos.
    */
    qc , oc , qi , oi , qd , od <-- get_info_painel =
        BEGIN
            qc := SIGMA zz . ( zz : dom ( tipo ) & tipo ( zz ) = comum | 1 ) ;
            oc := SIGMA zz . ( zz : dom ( tipo ) & tipo ( zz ) = comum &
                              zz : dom ( status ) & status ( zz ) = ocupada | 1 ) ;

            qi := SIGMA zz . ( zz : dom ( tipo ) & tipo ( zz ) = idoso | 1 ) ;
            oi := SIGMA zz . ( zz : dom ( tipo ) & tipo ( zz ) = idoso &
                              zz : dom ( status ) & status ( zz ) = ocupada | 1 ) ;

            qd := SIGMA zz . ( zz : dom ( tipo ) & tipo ( zz ) = deficiente | 1 ) ;
            od := SIGMA zz . ( zz : dom ( tipo ) & tipo ( zz ) = deficiente &
                              zz : dom ( status ) & status ( zz ) = ocupada | 1 )
        END ;

    vv <-- indicar ( tt ) =
        ANY uu
        WHERE uu : dom ( dom ( tipo |> { tt } ) <| status |> { livre } ) THEN
            vv := uu
        END
END
